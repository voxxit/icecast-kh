#!Nsis Installer Command Script
#
# This is an NSIS Installer Command Script generated automatically
# by the Fedora nsiswrapper program.  For more information see:
#
#   http://fedoraproject.org/wiki/MinGW
#
# To build an installer from the script you would normally do:
#
#   makensis this_script
#
# which will generate the output file 'installer.exe' which is a Windows
# installer containing your program.

Name "Icecast KH"
OutFile "icecast-2.4.0-kh15_win64_setup.exe"

InstallDir "$ProgramFiles64\Icecast KH"
InstallDirRegKey HKLM SOFTWARE\Icecast "Install_Dir"

LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"

ShowInstDetails hide
ShowUninstDetails hide

# Uncomment this to enable BZip2 compression, which results in
# slightly smaller files but uses more memory at install time.
SetCompressor bzip2

XPStyle on

;Version Information

VIProductVersion "2.4.0.15"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "Icecast KH"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductVersion" "2.4.0"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "Karl Heyes @2000-2020"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "Icecast KH streaming media server installer"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "2.4.0-KH15"

Page components
Page directory
Page instfiles

ComponentText "Select which optional components you want to install."

DirText "Please select the installation folder."

Function un.onInit
    MessageBox MB_YESNO "Uninstall icecast KH from $INSTDIR ?" IDYES NoAbort
      Abort ; causes uninstaller to quit.
    NoAbort:
FunctionEnd

Section "Icecast Installation"
  SectionIn RO

  SetOutPath "$INSTDIR"
  File /r "x64/*.dll"

  WriteUninstaller "$INSTDIR\Uninstall.exe"

  File "x64/icecast.exe"
  SetOverwrite off
  File "x64/icecast.xml"
  SetOverwrite on

  SetOutPath "$INSTDIR\logs"
  File /r "x64/logs/"

  SetOutPath "$INSTDIR\admin"
  File /r "x64/admin/"

  SetOutPath "$INSTDIR\doc"
  File /r "x64/doc/"

  SetOutPath "$INSTDIR\web"
  File /r "x64/web/"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Icecast KH" \
                 "DisplayName" "Icecast KH"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Icecast KH" \
                 "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
SectionEnd

Section "Install Service"
  SetOutPath "$INSTDIR"
  ExecWait '"$INSTDIR\icecast.exe" "install" "$INSTDIR"'
SectionEnd

Section "Start Menu Shortcuts"
  CreateDirectory "$SMPROGRAMS\Icecast KH"
  SetOutPath "$INSTDIR"
  CreateShortCut "$SMPROGRAMS\Icecast KH\Edit Configuration.lnk" "notepad.exe" "$INSTDIR\icecast.xml"
  CreateShortCut "$SMPROGRAMS\Icecast KH\Uninstall Icecast.lnk" "$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\Icecast KH\Run Icecast.lnk" "$INSTDIR\icecast.exe" '-c "$INSTDIR\icecast.xml"'
SectionEnd

Section "Desktop Icons"
  SetOutPath "$INSTDIR"
  CreateShortCut "$DESKTOP\Icecast.lnk" "$INSTDIR\icecast.exe" '-c "$INSTDIR\icecast.xml"'
SectionEnd

Section "Uninstall"
  ExecWait '"$INSTDIR\icecast.exe" "remove"'
  Delete $INSTDIR\Uninstall.exe
  Delete /rebootok "$DESKTOP\Icecast.lnk"
  Delete /rebootok "$SMPROGRAMS\Icecast KH\Run Icecast.lnk"
  Delete /rebootok "$SMPROGRAMS\Icecast KH\Uninstall Icecast.lnk"
  Delete /rebootok "$SMPROGRAMS\Icecast KH\Edit Configuration.lnk"
  RMDir "$SMPROGRAMS\Icecast KH"

  RMDir /r "$INSTDIR\web"
  RMDir /r "$INSTDIR\doc"
  RMDir /r "$INSTDIR\admin"

  Delete /rebootok "$INSTDIR\icecast.exe"

  Delete /rebootok "$INSTDIR\*.dll"

  RMDir "$INSTDIR"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Icecast KH"
SectionEnd
